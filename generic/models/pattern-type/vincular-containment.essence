given length : int
given vincular_containment : sequence of (sequence (injective) of int, set of int)

find perm : sequence (size length, injective) of int(1..length)
find vincular_containment_evidence : sequence (size |vincular_containment|) of sequence (maxSize length) of int(1..length)


$ the length of the evidence needs to match the length of the pattern
such that
    [ |ix| = |pattern|
    | (patternId, (pattern, bars)) <- vincular_containment
    , letting ix be vincular_containment_evidence(patternId)
    ]

such that
    [ forAll n : int(1..|pattern|-1) . ix(n) < ix(n+1)
    | (patternId, (pattern, bars)) <- vincular_containment
    , letting ix be vincular_containment_evidence(patternId)
    ]

such that
    [ forAll n1, n2 : int(1..|pattern|) , n1 < n2 .
        (pattern(n1) < pattern(n2) <-> perm(ix(n1)) < perm(ix(n2)))
    | (patternId, (pattern, bars)) <- vincular_containment
    , letting ix be vincular_containment_evidence(patternId)
    ]

such that
    [ forAll bar in bars . ix(bar) + 1 = ix(bar+1)
    | (patternId, (pattern, bars)) <- vincular_containment
    , letting ix be vincular_containment_evidence(patternId)
    ]

branching on [perm]